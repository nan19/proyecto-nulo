import java_cup.runtime.*;
import java.lang.*;
import java.util.*;

/**
 * Universidad Simon Bolivar
 * Lenguajes de Programacion II
 * Entrega final 7/04/2008
 * 
 * Maria Sol Ferrer 04-36975
 * Jamil Navarro 04-37334
 *
 * Proyecto: Procesador de lenguaje imperativo.
 * Leng2.cup: Analizador sintactico del lenguaje
 * 
 */
 
 /*
	Llamadas a procs
	Tam de arreglos
	Def de tipos nuevos
	Verificacion de errores de llamadas a procs
	Verificacion de errores de  tipos compuestos
	Verificacion de errores de  tipos nuevos, definidos antes de usar
 */
action code {:	
	int ib = 0;
    Bloque bloque = (Misc.acumulador != null)? Misc.acumulador : new Bloque(++ib);	
	boolean noError = true;	
	boolean proc = false;
	
:}

/**
 * Definicion de los tokens generados por el escaner
 * con el tipo asociado.
 */  
 
terminal TokenValue FUNC;
terminal TokenValue FINFUNC;
terminal TokenValue PROC;
terminal TokenValue FINPROC;
terminal TokenValue ENTRADA;
terminal TokenValue ENTSAL;
terminal TokenValue SALIDA;
terminal TokenValue DOSPUNTOS;
terminal TokenValue COMIENZO;
terminal TokenValue FIN;
terminal TokenValue DECLENTERO;
terminal TokenValue DECLREAL;
terminal TokenValue DECLBOOL;
terminal TokenValue REGISTRO;
terminal TokenValue FINREG;
terminal TokenValue TIPODATO;
terminal TokenValue FINT;
terminal TokenValue CASO;
terminal TokenValue ES;
terminal TokenValue APARENT;
terminal TokenValue CPARENT;
terminal TokenValue ACORCH;
terminal TokenValue CCORCH;  
terminal TokenValue COMA;  
terminal TokenValue PUNTO;  
terminal OperadorB MAS;
terminal OperadorB MENOS;
terminal OperadorB POR;
terminal OperadorB ENTRE;
terminal OperadorB DIV;
terminal OperadorB MOD; 
terminal OperadorU TECHO;
terminal OperadorU PISO;
terminal OperadorU REDONDEO;
terminal OperadorB YLOG;
terminal OperadorB OLOG;
terminal OperadorU NOLOG;
terminal OperadorB IGUAL;
terminal OperadorB DIF;
terminal OperadorB MAYOR;
terminal OperadorB MENOR;
terminal OperadorB MAYORIGUAL;
terminal OperadorB MENORIGUAL;
terminal TokenValue ASIGNACION; 
terminal TokenValue SI;
terminal TokenValue SINO;
terminal TokenValue FINSI;
terminal TokenValue HACER;
terminal TokenValue FINHACER;
terminal TokenValue PYCOMA;
terminal TokenValue CIERTO;
terminal TokenValue FALSO;
		
terminal Integer ENTERO;
terminal TokenValue ID;
terminal Float REAL;

/**
 * Definicion de los simbolos no terminales de la
 * gramatica con el tipo de su atributo asociado.
 */
non terminal inicio;
non terminal Bloque bloq;
non terminal Bloque lInst;
non terminal Inst inst, Decl, SubProg, iDeclA, iAsig, iSi, iSino, 
				iHacer, iDefTipo;
non terminal InstLlamada iLlamada;
non terminal Expresion expr, fact;
non terminal Bloque inicB, finB;
non terminal Tipo tipo, tipoG, tipoArr, tipoReg, tipoVan;
non terminal TipoF tipoB;
non terminal LinkedList arreglo, Lelem, LParam;
non terminal Proc;
non terminal Func;
non terminal Metodo Metodo;
non terminal LinkedList LVar;
non terminal iniBProc;
non terminal LCorchN;
non terminal LCorch;
non terminal TokenValue Var;
non terminal LValue LVal;
non terminal LSubProg;
non terminal HashMap lDiscr;
non terminal TablaSim LC;

/**
  * Orden de Precedencia
  * Los operadores mas abajo tienen mayor precedencia
  */
precedence left IGUAL, DIF;
precedence left OLOG;
precedence left YLOG;
precedence left MAYOR, MENOR, MAYORIGUAL, MENORIGUAL;
precedence left MAS, MENOS;
precedence left POR, ENTRE, DIV, MOD;
precedence left PUNTO;

/**
 * Gramatica del lenguaje.
 */
inicio	::= LSubProg COMIENZO bloq:b FIN
        {: 	//if (noError)
				//System.out.println("INICIO bloque: "+bloque.getId());
				b.imprimirB(0);
			
		:}
		;

/** Definicion de un Bloque */
bloq    ::= inicB lInst finB:b {: RESULT = b; :}
        ;
/** Inicio de un Bloque */
inicB   ::= {: 	//System.out.println("INICBLOQUE ANTES bloque: "+bloque.getId());
				if (!proc)
					bloque = new Bloque(bloque, true, ++ib); 
				proc = false;
				//System.out.println("INICBLOQUE DESPUES bloque: "+bloque.getId());:}
        ;
/** Fin de un Bloque */
finB    ::= {: 	Bloque b = bloque; 
				if (bloque.getParent() != null)
					bloque = bloque.getParent(); 
				RESULT = b; 
				
				//System.out.println("FINBLOQUE bloque: "+bloque.getId());
			:}
        ;
/** Lista de Instrucciones */
lInst	::= lInst inst:i 	{: bloque.agregarInst(i); :}
        |   inst:i 		    {: bloque.agregarInst(i); :}
        ;
/** Tipos de Instrucciones */		
inst	::= Decl:i	PYCOMA  	{: RESULT = i; :}
        |   iDeclA:i	PYCOMA  {: RESULT = i; :}
        |   iAsig:i     PYCOMA  {: RESULT = i; :}
        |   iSi:i               {: RESULT = i; :}
        |   iSino:i             {: RESULT = i; :}
        |   iHacer:i            {: RESULT = i; :}
        |   iLlamada:i	PYCOMA  {: RESULT = i; :}	
		|	iDefTipo			{: RESULT = null; :}
        ;
/** Declaracion */
Decl	::= tipo:t ID:i
		{:  Decl inst = new Decl(t, i.text );	
			Informacion info = new Informacion(i.text,t,null, 0);			
			noError = noError && inst.esCorrecta(bloque,info, i.lineBegin+1);			
			if (bloque.getTS().getLocally(i.text) == null) {
				bloque.getTS().add(i.text ,info);
            }
			RESULT = null;
        :}				
        ;

/** Declaracion con Asignacion*/
iDeclA	::= tipo:t ID:i ASIGNACION:a expr:e
		{:  Decl inst = new Decl(t, i.text );
			if (t.getClass().toString().equals("class TArreglo")) {
				((TArreglo)t).setSize(((Arreglo)e).getTam());
				((TArreglo)t).setTipo(((TArreglo)(((Arreglo)e).getTipo(bloque))).getTipo());
			}
			Informacion info = new Informacion(i.text,t,e,0);
			noError = noError && inst.esCorrecta(bloque, info, a.lineBegin+1);
			if (bloque.getTS().get(i.text) == null){
                            bloque.getTS().add(i.text,info);
                        }
			InstAsig insta = new InstAsig(new Identificador(i.text), e); 					
			noError = noError && insta.esCorrecta(bloque, info, a.lineBegin+1);				
			RESULT = insta;     
			//chequear que el LValue exista
        :}
        ;
/** Instruccion de Asignacion */
iAsig	::= LVal:d ASIGNACION:a expr:e
        {: 	InstAsig i = new InstAsig(d , e);
			Informacion info = bloque.getTS().get(d.obtenerId());
			boolean aux = i.esCorrecta(bloque, info, a.lineBegin+1);
			if (aux) {
				//System.out.println("Si es correcta para "+d.obtenerId());				
				Tipo t = info.getTipo();
				if (t.getClass().toString().equals("class TArreglo")) {
					((TArreglo)t).setSize(((Arreglo)e).getTam());
					((TArreglo)t).setTipo(((TArreglo)(((Arreglo)e).getTipo(bloque))).getTipo());
					info.setTipo(t);
				}				
			}
			noError = noError && aux;
			
			RESULT = i; 
			
			//chequear que el LValue exista
        :}
        ;
/** Instruccion Condicional */		
iSi	::= SI APARENT expr:e CPARENT bloq:l FINSI
		{: 	System.out.println("IF bloque: "+bloque.getId());
			RESULT = new InstIf(e, l ); :}
        ;
/** Instruccion Condicional con flujo alterno */		
iSino 	::= SI APARENT expr:e CPARENT bloq:l1 SINO bloq:l2 FINSI
		{: RESULT = new InstIfElse(e, l1,l2); :}
        ;
/** Instruccion de Iteracion */		
iHacer	::= HACER APARENT expr:e CPARENT bloq:l FINHACER
		{: RESULT = new InstDo(e, l); :}
        ;
/** Instruccion de llamada a un procedimiento */		
iLlamada::=	ID:i APARENT LParam:l CPARENT
		{:	
			if (!bloque.getTS().isDefined(i.text)) {
				noError = noError & false;
				System.out.println("ERROR (linea "+(i.lineBegin+1)+
					") El subprograma '"+i.text+"' no ha sido definido.");
			} else {
				Informacion info = bloque.getTS().get(i.text);
				InfoSub is = (InfoSub)info.getValor();
				boolean ok = is.chequearParams(l, bloque,i.lineBegin+1);
				noError = noError && ok;					
			}	
				RESULT = new InstLlamada(i.text, l);
			
		:}
		;
		
iDefTipo::=	TIPODATO ID:i IGUAL tipo:t FINT
		{:
			InfoDefTipo inf = new InfoDefTipo(i.text, t);
			Tipo tipo = new TBasico(TipoF.NEWTYPE);
			Informacion info = new Informacion(i.text, tipo, inf, 0);
			bloque.getTS().add(i.text, info);
		:}
		;
		
/**Lista de Sibprogramas*/
LSubProg::=	SubProg LSubProg
		|
		;
		
/** Definicion de subprograma */		
SubProg	::=	Proc	{: RESULT = null; :}
		|	Func	{: RESULT = null; :}
		;
/** Definicion de Procedimiento */				
Proc	::= PROC ID:i iniBProc APARENT LVar CPARENT lInst:l finB:b FINPROC
			{:	
				if (bloque.getTS().get(i.text) != null) {
					Informacion info = bloque.getTS().get(i.text);
					InfoSub is = (InfoSub)info.valor;				
					is.setBloque(b);
					info.setValor(is);
					bloque.getTS().add(i.text, info);
					proc = true;
				} 				
			:}
		;
/** Definicion de Funcion */					
Func	::= FUNC tipo ID:i iniBProc APARENT LVar CPARENT lInst:l finB:b FINFUNC
			{:	if (bloque.getTS().get(i.text) != null) {
					Informacion info = bloque.getTS().get(i.text);
					InfoSub is = (InfoSub)info.valor;				
					is.setBloque(b);
					info.setValor(is);						
					bloque.getTS().add(i.text, info);
				}
			:}
		;
/** Creacion del bloque de un subprograma */	
iniBProc::=	{: 	
				bloque = new Bloque(bloque, false, ++ib); 
			:}
		;

LVar	::=	LVar:lv COMA tipo:t ID:i DOSPUNTOS Metodo:m
			{:
				Informacion info = new Informacion(i.text, t, null, 0);
				bloque.getTS().add(i.text, info);
				
			:}
		|	tipo:t ID:i DOSPUNTOS Metodo:m
			{:
				Informacion info = new Informacion(i.text, t, null, 0);
				bloque.getTS().add(i.text, info);
				
			:}
		;

Metodo	::=	ENTRADA		{: RESULT = Metodo.E; :}
		|	SALIDA		{: RESULT = Metodo.S; :}
		|	ENTSAL		{: RESULT = Metodo.ES; :}
		;
		
/** Definicion de Expresion */	
expr	::= expr:e1 IGUAL expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.IGUAL, e2); :}
        |   expr:e1 DIF expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.DESIGUAL, e2); :}
        |   expr:e1 OLOG expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.OR, e2); :}
        |   expr:e1 YLOG expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.AND, e2); :}
        |   expr:e1 MAYOR expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.MAYOR ,e2); :}
        |   expr:e1 MENOR expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.MENOR ,e2); :}
        |   expr:e1 MAYORIGUAL expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.MAYORIGUAL ,e2); :}
        |   expr:e1 MENORIGUAL expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.MENORIGUAL ,e2); :}
        |   expr:e1 MAS expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.SUMA ,e2); :}
        |   expr:e1 MENOS expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.RESTA ,e2); :}
        |   expr:e1 ENTRE expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.DIVR ,e2); :}
        |   expr:e1 POR expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.MULT ,e2); :}
        |   expr:e1 DIV expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.DIVE ,e2); :}
        |   expr:e1 MOD expr:e2
		{:  RESULT = new ExprBin(e1, OperadorB.MOD ,e2); :}
        |   fact:f
		{:	RESULT = f; :}		
        |   expr:e PUNTO expr   //Crear algo que represente miembro interno
		{:	RESULT = e; :}
        ;
/** Definicion de Factor */		
fact	::= APARENT expr:e CPARENT 
		{:  RESULT = e; :}
        |   ENTERO:n 
		{:  RESULT = new Factor (TipoF.INT, n); :}
        |   MENOS fact:f 
		{:  RESULT = new ExprUna (OperadorU.MENOS, f); :}
        |   REAL:n 
		{:  RESULT = new Factor (TipoF.FLOAT, n); :}
        |   TECHO APARENT fact:f CPARENT
		{:  RESULT = new ExprUna (OperadorU.TECHO, f); :}
        |   PISO APARENT fact:f CPARENT
		{:  RESULT = new ExprUna (OperadorU.PISO, f); :}
        |   REDONDEO APARENT fact:f CPARENT
		{:  RESULT = new ExprUna (OperadorU.REDONDEO, f); :}
        |   NOLOG fact:f
		{:  RESULT = new ExprUna (OperadorU.NOT, f); :}
        |   LVal:i
		{:  Factor e = new Factor (TipoF.LVAL, i); 	//cambiar
			noError = noError && e.esCorrecta(bloque, 0); 	//pasar numero de linea
			RESULT = e;
		:}
        |   CIERTO:c 
		{: RESULT= new Factor(TipoF.BOOL, new Booleano(true)); :}
        |   FALSO:c 
		{: RESULT= new Factor(TipoF.BOOL, new Booleano(false)); :}		
        |   arreglo:l
		{: 	
			Tipo t = ((Expresion)(l.get(0))).getTipo(bloque);	//todos deberian ser del mismo tipo
			RESULT = new Arreglo(t, 0, l); 
		:}
		|	iLlamada:i
		{:	bloque.getTS().get(i.getId());	//chequear que solo sea FUNC, devolver factor del tipo de la func
		:}
        ;

LVal	::=	LVal:l ACORCH expr:e CCORCH 	// expr debe ser de tipo INT
		{:	RESULT = new ElemArreglo(l, e); :}
		|	LVal:l PUNTO ID:i				// ID debe ser campo del record
		{:	RESULT = new CampoRegistro(l, i.text); :}
		|	ID:i
		{: RESULT = new Identificador(i.text); :}
		;
		
/** Lista de parametros que se pasan al llamar a un subprograma */
LParam	::=	LParam:l COMA expr:e
		{:  l.add(e);
			RESULT = l;
		:}
		|	expr:e
		{:  LinkedList<Expresion> l = new LinkedList<Expresion>();
			l.add(e);
			RESULT = l;
		:}
		;

/** Tipos de Datos */		
tipo    ::= tipoB:t     
			{: RESULT = new TBasico(t); :}
        |   ACORCH ENTERO:i CCORCH tipo:t   
			{: RESULT = new TArreglo(t,i); :}	//tamanio del arreglo?
        |   ACORCH CCORCH tipo:t   
			{: RESULT = new TArreglo(t,-1); :}
        |   REGISTRO LC:ts FINREG
			{: RESULT = new TRegistro(ts); :}
		|	REGISTRO LC:ts COMA CASO tipo:t ID:i ES lDiscr:hm FINREG //Lvar ts comun
			{: 
				Informacion info = new Informacion(i.text, t, null, 0);
				ts.add(i.text,info);
				RESULT = new TVariante(ts,i.text, (HashMap<Object,TablaSim>)hm); 
			:}
		|	ID:i
			{: //chequear en TS y obtener arbol de tipo
			
			:}
        ;

/** Tipos Basicos */
tipoB   ::= DECLENTERO:t    {: RESULT = TipoF.INT; :}
        |   DECLREAL:t      {: RESULT = TipoF.FLOAT;  :}
        |   DECLBOOL:t      {: RESULT = TipoF.BOOL; :} 
        ;

/** Campos de un Registro*/
LC		::=	tipo:t ID:i				
		{: 	TablaSim ts = new TablaSim();
			Informacion info = new Informacion(i.text, t, null,0);	
			ts.add(i.text,info);
			RESULT = ts; 
		:}
		|	LC:ts COMA tipo:t ID:i	
		{: 	Informacion info = new Informacion(i.text, t, null,0);		
			ts.add(i.text,info);
			RESULT = ts; 
		:}
        ;

/** Definicion de casos del discriminante */
lDiscr	::= lDiscr:hm fact:f APARENT LC:ts CPARENT
			{: 
				if(!hm.containsKey(f)){
					hm.put(f,ts);
				}
				RESULT = hm;
			:}
        |   fact:f APARENT LC:ts CPARENT
			{:
				HashMap<Object,TablaSim> hm = new HashMap<Object,TablaSim>();
				hm.put(f,ts);
				RESULT = hm;
			:}
        ;

/** Contenido de un arreglo */	
arreglo	::= ACORCH Lelem:l CCORCH		//chequear q sean del mismo tipo los elem
		{: RESULT = l; :}
		;

/** Lista de elementos */			
Lelem	::= Lelem:L COMA fact:f			
		{:	L.add(f);
				RESULT = L;
		:}
        |	fact:f
		{: 	LinkedList<Expresion> L = new LinkedList<Expresion>();
				L.add(f);
				RESULT = L;
		:}
		;

